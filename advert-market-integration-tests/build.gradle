plugins {
    id 'java-library'
    id 'io.spring.dependency-management'
}

dependencies {
    api platform(project(':platform-bom'))

    testImplementation project(':advert-market-shared')
    testImplementation project(':advert-market-db')
    testImplementation project(':advert-market-app')
    testImplementation project(':advert-market-identity')
    testImplementation project(':advert-market-financial')
    testImplementation project(':advert-market-marketplace')
    testImplementation project(':advert-market-deal')
    testImplementation project(':advert-market-delivery')
    testImplementation project(':advert-market-communication')

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-jdbc'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-redis'
    testImplementation 'org.springframework.boot:spring-boot-starter-validation'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:testcontainers'
    testImplementation 'org.testcontainers:testcontainers-postgresql'
    testImplementation 'org.testcontainers:testcontainers-kafka'
    testImplementation 'org.testcontainers:testcontainers-junit-jupiter'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'com.tngtech.archunit:archunit-junit5'
    testImplementation 'io.micrometer:micrometer-core'
    testImplementation 'org.liquibase:liquibase-core'
    testImplementation 'org.postgresql:postgresql'
    testImplementation 'org.jooq:jooq'
    testImplementation 'org.springframework.boot:spring-boot-starter-jooq'
    testImplementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
    testImplementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.kafka:spring-kafka'
    testImplementation 'com.github.pengrad:java-telegram-bot-api'
    testImplementation 'io.github.resilience4j:resilience4j-circuitbreaker'
    testImplementation 'io.github.resilience4j:resilience4j-bulkhead'
    testImplementation 'com.github.ben-manes.caffeine:caffeine'
}

// Integration tests are opt-in to keep default build non-blocking.
tasks.named('test') {
    onlyIf { project.findProperty('runIntegrationTests') == 'true' }
    useJUnitPlatform {
        excludeTags 'bot-hardening'
    }
}

tasks.named('checkstyleTest') {
    onlyIf { project.findProperty('runIntegrationTests') == 'true' }
}

tasks.register('botHardeningTest', Test) {
    description = 'Runs bot hardening integration/performance suite.'
    group = 'verification'
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    useJUnitPlatform {
        includeTags 'bot-hardening'
    }
    shouldRunAfter(tasks.named('test'))
    onlyIf { project.findProperty('runIntegrationTests') == 'true' }
}
