plugins {
    id 'java'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'org.springdoc.openapi-gradle-plugin'
}

ext['jooq.version'] = '3.20.11'

dependencies {
    implementation platform(project(':platform-bom'))

    implementation project(':advert-market-shared')
    implementation project(':advert-market-db')
    implementation project(':advert-market-identity')
    implementation project(':advert-market-financial')
    implementation project(':advert-market-marketplace')
    implementation project(':advert-market-deal')
    implementation project(':advert-market-delivery')
    implementation project(':advert-market-communication')

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-json'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.springframework.boot:spring-boot-starter-liquibase'
    implementation 'org.springframework.kafka:spring-kafka'

    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'net.logstash.logback:logstash-logback-encoder'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui'
    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

jar {
    enabled = true
}

tasks.named('processResources').configure {
    // Spring MessageSource in an executable bootJar expects bundles on the main classpath.
    // Copy bot/notification bundles into this module so they end up under BOOT-INF/classes/messages/*.
    from(project(':advert-market-communication').sourceSets.main.resources) {
        include 'messages/*.properties'
    }
}

tasks.named('test').configure {
    // Packaging assertions read the produced bootJar.
    dependsOn tasks.named('bootJar')
}

springBoot {
    mainClass = 'com.advertmarket.app.AdvertMarketApplication'
}

openApi {
    apiDocsUrl = 'http://localhost:8080/v3/api-docs.yaml'
    outputDir = file("${rootDir}/api")
    outputFileName = 'openapi.yml'
}

tasks.named('forkedSpringBootRun').configure {
    dependsOn(
            ':advert-market-shared:jar',
            ':advert-market-db:jar',
            ':advert-market-identity-api:jar',
            ':advert-market-identity:jar',
            ':advert-market-financial-api:jar',
            ':advert-market-financial:jar',
            ':advert-market-marketplace-api:jar',
            ':advert-market-marketplace:jar',
            ':advert-market-deal-api:jar',
            ':advert-market-deal:jar',
            ':advert-market-delivery-api:jar',
            ':advert-market-delivery:jar',
            ':advert-market-communication-api:jar',
            ':advert-market-communication:jar'
    )
}
